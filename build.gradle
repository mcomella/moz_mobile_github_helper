buildscript {
    ext.kotlin_version = '1.2.0'
    ext.ktor_version = '0.9.1-alpha-8' // alpha needed for kotlin 1.2 support.

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

apply plugin: 'application'

mainClassName = 'app.AppKt'

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // We choose ktor because it's kotlin-based and recommended by JetBrains,
    // but there are many other good Java choices.
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "ch.qos.logback:logback-classic:1.2.1" // ktor logger.

    testCompile 'junit:junit:4.12'
}

jar {
    baseName = 'app' // todo: more descriptive
    version = '0.0.1' // todo: where else set version?

    manifest.attributes(
            "Main-Class": mainClassName,
    )

    // Create fat jar, via
    // https://medium.com/@preslavrachev/kotlin-basics-create-executable-kotlin-jars-using-gradle-d17e9a8384b9
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

// Copy to an unversioned JAR so we don't have to update the jar
// version in the procfile on update.
task stage(type: Copy, dependsOn: [clean, build]) {
    from jar.archivePath
    into 'build/stage/'
    rename {
        'app.jar'
    }
}
stage.mustRunAfter(clean)
